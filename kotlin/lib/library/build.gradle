plugins {
    id 'org.jetbrains.kotlin.android'
    id 'com.android.library'
}

android {
    namespace 'com.xplpc.library'
    compileSdk 33
    buildToolsVersion '30.0.3'
    ndkVersion '25.1.8937393'

    defaultConfig {
        minSdk 16
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                // cxx flags
                cppFlags '-std=c++17'

                // cmake arguments
                def cmakeArguments = []
                cmakeArguments.add('-DXPLPC_TARGET=kotlin')
                cmakeArguments.add('-DXPLPC_ADD_CUSTOM_DATA=ON')
                cmakeArguments.add('-DXPLPC_DEPENDENCY_TOOL=' + (project.hasProperty('xplpc_dependency_tool') ? project.property('xplpc_dependency_tool') : 'cpm'))
                cmakeArguments.add('-DXPLPC_ENABLE_INTERFACE=' + (project.hasProperty('xplpc_interface') ? 'YES' : 'NO'))

                def cmakeStringArguments = new String[cmakeArguments.size()]
                for (int i = 0; i < cmakeArguments.size(); i++) {
                    cmakeStringArguments[i] = cmakeArguments.get(i)
                }

                arguments(cmakeStringArguments)
            }
        }
    }

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip '**/*.so'
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            packagingOptions {
                doNotStrip '**/*.so'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path file(project.projectDir.absolutePath + '/../../../CMakeLists.txt')
            version '3.24.0+'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'com.google.code.gson:gson:2.10'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
